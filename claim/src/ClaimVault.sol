// SPDX-License-Identifier: MIT
pragma solidity 0.8.28;

import {Ownable} from "@openzeppelin/contracts/access/Ownable.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {MessageHashUtils} from "@openzeppelin/contracts/utils/cryptography/MessageHashUtils.sol";
import {ECDSA} from "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";
import {SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import {Pausable} from "@openzeppelin/contracts/utils/Pausable.sol";
import {ReentrancyGuard} from "@openzeppelin/contracts/utils/ReentrancyGuard.sol";

/**
 * @title ClaimVault
 * @notice Immediate token claims authorized by an off-chain signer with per-epoch global/user caps.
 * @dev Uses personal-sign style hashing with explicit contract address in the payload.
 *      Protected by Pausable and ReentrancyGuard.
 * @custom:security-contact steam@zerobase.pro
 */
contract ClaimVault is Ownable, Pausable, ReentrancyGuard {
    using SafeERC20 for IERC20;

    /// @notice ERC20 token managed by this vault.
    IERC20 public immutable USDT;

    /// @notice Timestamp when claiming starts; used as the epoch anchor.
    uint256 public immutable startClaimTimestamp;

    /// @notice Address whose signatures authorize claims.
    address public signer;

    /// @notice Per-user nonce to prevent signature replay.
    mapping(address user => uint256 nonce) public userNonce;

    /// @notice Epoch length in seconds.
    uint256 public epochDuration;

    /// @notice Max total claimed per epoch across all users.
    uint256 public globalCapPerEpoch;

    /// @notice Max total claimed per epoch for a single user.
    uint256 public userCapPerEpoch;

    /// @notice Global claimed amounts: epochDuration => epochId => amount.
    mapping(uint256 epochDuration => mapping(uint256 epochId => uint256 claimedAmount))
        public claimedByEpoch;

    /// @notice Per-user claimed amounts: epochDuration => user => epochId => amount.
    mapping(uint256 epochDuration => mapping(address user => mapping(uint256 epochId => uint256 claimedAmount)))
        public userClaimedByEpoch;

    /**
     * @notice Emitted when a claim is successfully processed.
     * @param user Recipient address.
     * @param amount Claimed amount.
     * @param epochId Current epoch id.
     * @param currentEpochDuration Epoch length used for accounting.
     * @param userNonce User nonce consumed for this claim.
     */
    event Claimed(
        address indexed user,
        uint256 indexed amount,
        uint256 indexed epochId,
        uint256 currentEpochDuration,
        uint256 userNonce
    );

    /**
     * @notice Emitted when the owner withdraws tokens in emergencies.
     * @param _token Token address withdrawn.
     * @param _receiver Recipient address.
     */
    event EmergencyWithdrawal(
        address indexed _token,
        address indexed _receiver
    );

    /**
     * @notice Emitted when the signer address changes.
     * @param oldSigner Previous signer.
     * @param newSigner New signer.
     */
    event UpdateSigner(address indexed oldSigner, address indexed newSigner);

    /**
     * @notice Emitted when epoch configuration changes.
     * @param epochDuration New epoch length (seconds).
     * @param globalCapPerEpoch New global cap per epoch.
     * @param userCapPerEpoch New per-user cap per epoch.
     */
    event UpdateEpochConfig(
        uint256 indexed epochDuration,
        uint256 globalCapPerEpoch,
        uint256 userCapPerEpoch
    );

    // --- Custom Errors ---
    error ZeroAmount();
    error InvalidSender();
    error SignatureExpired();
    error InvalidSignature();
    error TokenZeroAddress();
    error ReceiverZeroAddress();
    error SignerZeroAddress();
    error EpochDurationZero();
    error GlobalCapZero();
    error UserCapInvalid();
    error GlobalCapExceeded();
    error UserCapExceeded();

    /**
     * @notice Initializes the vault.
     * @param _USDT Token address to manage.
     * @param _signer Off-chain signer that authorizes claims.
     * @param _epochDuration Epoch length (seconds).
     * @param _globalCapPerEpoch Global cap per epoch.
     * @param _userCapPerEpoch Per-user cap per epoch.
     */
    constructor(
        address _USDT,
        address _signer,
        uint256 _epochDuration,
        uint256 _globalCapPerEpoch,
        uint256 _userCapPerEpoch
    ) Ownable(msg.sender) {
        USDT = IERC20(_USDT);
        signer = _signer;
        startClaimTimestamp = block.timestamp;
        epochDuration = _epochDuration;
        globalCapPerEpoch = _globalCapPerEpoch;
        userCapPerEpoch = _userCapPerEpoch;
        emit UpdateSigner(address(0), _signer);
        emit UpdateEpochConfig(
            _epochDuration,
            _globalCapPerEpoch,
            _userCapPerEpoch
        );
    }

    /**
     * @notice Owner-only emergency token sweep.
     * @param _token Token address to withdraw.
     * @param _receiver Recipient of the withdrawn balance.
     */
    function emergencyWithdraw(
        address _token,
        address _receiver
    ) external onlyOwner {
        require(_token != address(0), TokenZeroAddress());
        require(_receiver != address(0), ReceiverZeroAddress());

        IERC20(_token).safeTransfer(
            _receiver,
            IERC20(_token).balanceOf(address(this))
        );
        emit EmergencyWithdrawal(_token, _receiver);
    }

    /**
     * @notice Updates the signer address.
     * @param _newSigner New signer (must be non-zero).
     */
    function setSigner(address _newSigner) external onlyOwner {
        require(_newSigner != address(0), SignerZeroAddress());
        address oldSigner = signer;
        signer = _newSigner;
        emit UpdateSigner(oldSigner, _newSigner);
    }

    /**
     * @notice Updates epoch length and caps.
     * @dev Per-user cap must be > 0 and <= global cap.
     * @param _epochDuration Epoch length in seconds.
     * @param _globalCapPerEpoch Global cap per epoch.
     * @param _userCapPerEpoch Per-user cap per epoch.
     */
    function setEpochConfig(
        uint256 _epochDuration,
        uint256 _globalCapPerEpoch,
        uint256 _userCapPerEpoch
    ) external onlyOwner {
        require(_epochDuration > 0, EpochDurationZero());
        require(_globalCapPerEpoch > 0, GlobalCapZero());

        require(
            _userCapPerEpoch > 0 && _userCapPerEpoch <= _globalCapPerEpoch,
            UserCapInvalid()
        );

        epochDuration = _epochDuration;
        globalCapPerEpoch = _globalCapPerEpoch;
        userCapPerEpoch = _userCapPerEpoch;
        emit UpdateEpochConfig(
            _epochDuration,
            _globalCapPerEpoch,
            _userCapPerEpoch
        );
    }

    /// @notice Pauses claiming (owner-only).
    function pause() external onlyOwner {
        _pause();
    }

    /// @notice Unpauses claiming (owner-only).
    function unpause() external onlyOwner {
        _unpause();
    }

    /**
     * @notice Claim tokens immediately using a valid off-chain signature.
     * @dev Verifies signature over (user, amount, nonce, chainId, expiry, address(this)).
     *      Increments user nonce after successful verification.
     *      Enforces global and per-user epoch caps.
     * @param user Must equal msg.sender.
     * @param claimAmount Amount to claim.
     * @param expiry Signature expiry timestamp (must be > block.timestamp).
     * @param signature Signer's signature.
     */
    function claim(
        address user,
        uint256 claimAmount,
        uint256 expiry,
        bytes calldata signature
    ) external whenNotPaused nonReentrant {
        require(claimAmount != 0, ZeroAmount());
        require(user == msg.sender, InvalidSender());
        require(expiry > block.timestamp, SignatureExpired());

        uint256 currentEpochDuration = epochDuration;
        uint256 currentUserNonce = userNonce[msg.sender];

        uint256 chainId;
        assembly {
            chainId := chainid()
        }

        bytes32 claimDigestHash = calculateClaimUSDTHash(
            msg.sender,
            claimAmount,
            currentUserNonce,
            chainId,
            expiry
        );

        require(
            _checkSignature(claimDigestHash, signature),
            InvalidSignature()
        );

        unchecked {
            userNonce[msg.sender] = currentUserNonce + 1;
        }

        uint256 epochId = currentEpochId();

        uint256 globalUsed = claimedByEpoch[currentEpochDuration][epochId];
        require(
            globalUsed + claimAmount <= globalCapPerEpoch,
            GlobalCapExceeded()
        );

        uint256 userUsed = userClaimedByEpoch[currentEpochDuration][msg.sender][
            epochId
        ];
        require(userUsed + claimAmount <= userCapPerEpoch, UserCapExceeded());

        unchecked {
            claimedByEpoch[currentEpochDuration][epochId] =
                globalUsed +
                claimAmount;
            userClaimedByEpoch[currentEpochDuration][msg.sender][epochId] =
                userUsed +
                claimAmount;
        }

        USDT.safeTransfer(msg.sender, claimAmount);
        emit Claimed(
            msg.sender,
            claimAmount,
            epochId,
            currentEpochDuration,
            currentUserNonce
        );
    }

    /**
     * @notice Returns the current epoch id based on start timestamp and epoch duration.
     * @return epochId Current epoch index.
     */
    function currentEpochId() public view returns (uint256) {
        return (block.timestamp - startClaimTimestamp) / epochDuration;
    }

    /**
     * @notice Computes the personal-sign style digest used by the contract.
     * @dev Equivalent to keccak256(abi.encode(...)) followed by toEthSignedMessageHash.
     * @param _user Claiming user.
     * @param _claimAmount Amount authorized.
     * @param _userNonce Expected user nonce.
     * @param _chainid Chain id for domain separation.
     * @param _expiry Expiry timestamp.
     * @return digest 32-byte message hash to be signed/verified.
     */
    function calculateClaimUSDTHash(
        address _user,
        uint256 _claimAmount,
        uint256 _userNonce,
        uint256 _chainid,
        uint256 _expiry
    ) public view returns (bytes32) {
        bytes32 userClaimUSDTStructHash = keccak256(
            abi.encode(
                _user,
                _claimAmount,
                _userNonce,
                _chainid,
                _expiry,
                address(this)
            )
        );
        return MessageHashUtils.toEthSignedMessageHash(userClaimUSDTStructHash);
    }

    /**
     * @notice Verifies that a signature was produced by the configured signer.
     * @param digestHash Message hash (already prefixed) that was signed.
     * @param signature Signature bytes (r||s||v).
     * @return result True if signature is valid.
     */
    function _checkSignature(
        bytes32 digestHash,
        bytes memory signature
    ) private view returns (bool result) {
        address recovered = ECDSA.recover(digestHash, signature);
        result = recovered == signer;
    }
}
